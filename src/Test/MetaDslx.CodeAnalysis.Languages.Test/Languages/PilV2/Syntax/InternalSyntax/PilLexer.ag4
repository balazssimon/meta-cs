lexer grammar PilLexer;

options
{
	generateCompiler=true;
}

channels
{
	COMMENT,
	WHITESPACE
}

// Keywords
$Token(first=KTypeDef,last=KNull,kind=ReservedKeyword)
KTypeDef: 'TYPE';
KEnum: 'ENUM';

KFunction: 'FUNCTION';
KEndFunction: 'EFUNCTION';
KResult: 'RESULT';

KFork: 'FORK';
KEndFork: 'EFORK';
KCase: 'CASE';
KElse: 'ELSE';

KIf: 'IF';
KEndIf: 'EIF';

KQuery: 'QUERY';
KEndQuery: 'EQUERY';
KPulse: 'PULSE';
KStatic: 'STATIC';

KObject: 'OBJECT';
KEndObject: 'EOBJECT';

KTrigger: 'TRIGGER';
KInput: 'INPUT';
KOnAccepted: 'ON_ACCEPTED';
KOnRefused: 'ON_REFUSED';
KOnCancel: 'ON_CANCELLED';

KAssert: 'ASSERT';
KRequest: 'REQ';
KAccept: 'ACCEPT';
KRefuse: 'REFUSE';
KCancel: 'CANCEL';

KVar: 'VAR';
KParam: 'PARAM';

KUndo: 'UNDO';

KTrue: 'TRUE';
KFalse: 'FALSE';

KInt: 'int';
KBool: 'bool';
KString: 'string';
KObjectType: 'object';

KIn: 'in';

KNull: 'NULL';


TSemicolon : ';';
TColon : ':';
TDot : '.';
$Token(defaultSeparator=true)
TComma : ',';
TAssign : ':=';
TOpenParen : '(';
TCloseParen : ')';
TOpenBracket : '[';
TCloseBracket : ']';
TOpenBrace : '{';
TCloseBrace : '}';
TLessThan : '<';
TGreaterThan : '>';
TQuestion : '?';

TQuestionQuestion : '??';
TAmpersand : '&';
THat : '^';
TBar : '|';
TAndAlso : '&&';
TOrElse : '||';
TPlusPlus : '++';
TMinusMinus : '--';
TPlus : '+';
TMinus : '-';
TTilde : '~';
TExclamation : '!';
TSlash : '/';
TAsterisk : '*';
TPercent : '%';
TLessThanOrEqual : '<=';
TGreaterThanOrEqual : '>=';
TEqual : '=';
TNotEqual : '!=';
TAsteriskAssign : '*=';
TSlashAssign : '/=';
TPercentAssign : '%=';
TPlusAssign : '+=';
TMinusAssign : '-=';
TLeftShiftAssign : '<<=';
TRightShiftAssign : '>>=';
TAmpersandAssign : '&=';
THatAssign : '^=';
TBarAssign : '|=';


$Token(kind=Identifier, defaultIdentifier=true)
LIdentifier : IdentifierBegin IdentifierCharacter*;
fragment IdentifierBegin : [a-zA-Z_];
fragment IdentifierCharacter : [a-zA-Z0-9_];

$Token(kind=Number)
LInteger : DecimalDigits | Hexadecimal;
$Token(kind=Number)
LDecimal : DecimalDigit+ '.' DecimalDigit+;
$Token(kind=Number)
LScientific : LDecimal [eE] Sign? DecimalDigit+;
fragment DecimalDigits : DecimalDigit+;
fragment DecimalDigit : [0-9];
fragment Sign : '+' | '-';
fragment Hexadecimal : ('0x'|'0X') HexDigit*;
fragment HexDigit : [0-9a-fA-F];

$Token(kind=String)
LString
    : '"' DoubleQuoteTextCharacter* '"'
    | '\'' SingleQuoteTextCharacter* '\'';

fragment SingleQuoteTextCharacter 
    : SingleQuoteTextSimple | CharacterEscapeSimple | CharacterEscapeUnicode;
fragment SingleQuoteTextSimple 
    : ~('\'' | '\\' | '\u000A' | '\u000D' | '\u0085' | '\u2028' | '\u2029');
fragment SingleQuoteTextVerbatimCharacter 
    : ~'\'' | SingleQuoteTextVerbatimCharacterEscape;
fragment SingleQuoteTextVerbatimCharacterEscape : '\'' '\'';
fragment SingleQuoteTextVerbatimCharacters : SingleQuoteTextVerbatimCharacter+;
fragment DoubleQuoteTextCharacter 
    : DoubleQuoteTextSimple | CharacterEscapeSimple | CharacterEscapeUnicode;
fragment DoubleQuoteTextSimple 
    : ~('"' | '\\' | '\u000A' | '\u000D' | '\u0085' | '\u2028' | '\u2029');
fragment DoubleQuoteTextVerbatimCharacter 
    : ~'"' | DoubleQuoteTextVerbatimCharacterEscape;
fragment DoubleQuoteTextVerbatimCharacterEscape : '"' '"';
fragment DoubleQuoteTextVerbatimCharacters : DoubleQuoteTextVerbatimCharacter+;
fragment CharacterEscapeSimple : '\\' CharacterEscapeSimpleCharacter;
fragment CharacterEscapeSimpleCharacter 
    : '\'' | '"' | '\\' | '0' | 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v'; 
fragment CharacterEscapeUnicode
    : '\\u' HexDigit HexDigit HexDigit HexDigit
    | '\\U' HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit;

// Whitespace and comments
$Token(kind=Whitespace)
LUtf8Bom : [\u00EF][\u00BB][\u00BF] -> channel(WHITESPACE);
$Token(kind=Whitespace, defaultWhitespace=true)
LWhiteSpace : [\u0020\u0009\u000B\u000C\u00A0\u001A]+ -> channel(WHITESPACE);
$Token(kind=Whitespace, endOfLine=true, defaultEndOfLine=true)
LCrLf : '\r'? '\n' -> channel(WHITESPACE);
$Token(kind=Whitespace, endOfLine=true)
LLineEnd : [\u0085\u2028\u2029] -> channel(WHITESPACE);

$Token(kind=GeneralComment)
LSingleLineComment : '//' ~[\r\n]* -> channel(COMMENT);
LMultiLineComment : '/*' .*? '*/' -> channel(COMMENT);

