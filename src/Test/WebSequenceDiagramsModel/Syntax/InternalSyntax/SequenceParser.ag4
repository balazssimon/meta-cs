parser grammar SequenceParser;

options {
	tokenVocab=SequenceLexer;
	generateCompiler=true;
}

main : interaction EOF;

$SymbolDef(Interaction)
interaction : line*;

line 
	: title LCrLf #titleLine
	| declaration? LCrLf #declarationLine;

$Scope
declaration
	: destroy
	| arrow
	| alt
	| opt
	| loop
	| ref
	| note
	;

title: KTitle name?;

$Property(Declarations)
$SymbolDef(Message)
arrow: $Property(Source) source=lifeLineName $Property(Kind) type=arrowType $Property(Target) target=lifeLineName TColon $Property(Text) $Value text?;

$Property(Declarations)
$SymbolDef(Destroy)
destroy: KDestroy $Property(Lifeline) lifeLineName?;

$Property(Declarations)
$SymbolDef(MultiFragment)
alt: altFragment elseFragment* end;

$Property(Fragments)
$SymbolDef(Fragment)
$Property(name=Kind, value=FragmentKind.Alt)
altFragment: KAlt $Property(Text) $Value condition=text? LCrLf fragmentBody;

$Property(Fragments)
$SymbolDef(Fragment)
$Property(name=Kind, value=FragmentKind.Else)
elseFragment: KElse $Property(Text) $Value condition=text? LCrLf fragmentBody;

$Property(Declarations)
$SymbolDef(Fragment)
$Property(name=Kind, value=FragmentKind.Loop)
loop: loopFragment end;
loopFragment: KLoop $Property(Text) $Value condition=text? LCrLf fragmentBody;

$Property(Declarations)
$SymbolDef(Fragment)
$Property(name=Kind, value=FragmentKind.Opt)
opt: optFragment end;
optFragment: KOpt $Property(Text) $Value condition=text? LCrLf fragmentBody;

$Property(Declarations)
$SymbolDef(RefFragment)
$Property(name=Kind, value=FragmentKind.Ref)
ref: simpleRefFragment | messageRefFragment;

simpleRefFragment: KRef (over=text TColon)? $Property(Text) $Value refText=text? LCrLf simpleBody KEndRef;
messageRefFragment: refInput simpleBody refOutput;

$Property(Input)
$SymbolDef(Message)
refInput : $Property(Source) source=lifeLineName $Property(Kind) sourceType=arrowType KRef (over=text TColon)? $Property(Text) $Value message=text? LCrLf;
$Property(Output)
$SymbolDef(Message)
refOutput: KEndRef ignored=text? $Property(Kind) targetType=arrowType $Property(Target) target=lifeLineName TColon $Property(Text) $Value message=text?;

fragmentBody: line*;
end: KEnd text?;

note
	: singleLineNote
	| multiLineNote
	;

singleLineNote : KNote position=text TColon noteText=text;
multiLineNote : KNote simpleBody KEndNote;

simpleBody: simpleLine*;
simpleLine: text LCrLf;

arrowType : $Value(MessageKind.Sync) TSync | $Value(MessageKind.Async) TAsync | $Value(MessageKind.Return) TReturn | $Value(MessageKind.Create) TCreate;

$Property(name=Declarations, owner=CurrentScope)
$SymbolDef(symbolType=Lifeline, merge=true)
lifeLineName : name;

$Name
name : identifier;

$Identifier
identifier : text;

text : identifierPart+;

identifierPart: LIdentifier;


