lexer grammar CoreLexer;

options
{
	generateCompiler=true;
}

channels
{
	COMMENT,
	WHITESPACE
}

@lexer::members {
    public int _brackets = 0;
}

// Keywords
$Token(first=KNamespace,last=KStatic,kind=ReservedKeyword)
KNamespace : 'namespace';
KUsing : 'using';
KExtern : 'extern';
KAbstract : 'abstract';
KInterface : 'interface';
KClass : 'class';
KStruct : 'struct';
KEnum : 'enum';
KNew : 'new';
KNull : 'null';
KTrue : 'true';
KFalse : 'false';
KObject : 'object';
KVoid : 'void';
KBool : 'bool';
KChar : 'char';
KSByte : 'sbyte';
KByte : 'byte';
KShort : 'short';
KUShort : 'ushort';
KInt : 'int';
KUInt : 'uint';
KLong : 'long';
KULong : 'ulong';
KDecimal : 'decimal';
KFloat : 'float';
KDouble : 'double';
KString : 'string';
KTypeof : 'typeof';
KNameof : 'nameof';
KSizeof : 'sizeof';
KDefault : 'default';
KChecked : 'checked';
KUnchecked : 'unchecked';
KAs : 'as';
KIs : 'is';
KNot : 'not';
KThis : 'this';
KBase : 'base';
KConst : 'const';
KReadonly : 'readonly';
KStatic : 'static';

// Tokens
TSemicolon : ';';
TColon : ':';
TDot : '.';
$Token(defaultSeparator=true)
TComma : ',';
TAssign : '=';
TOpenParen : '(';
TCloseParen : ')';
TOpenBracket : '[' { _brackets++; };
TCloseBracket : ']' { _brackets--; };
TOpenBrace : '{';
TCloseBrace : '}';
TLessThan : '<';
TGreaterThan : '>';
TQuestion : '?';

TQuestionDot : '?.';
TQuestionOpenBracket : '?[';
TQuestionQuestion : '??';
TAmpersand : '&';
THat : '^';
TBar : '|';
TAndAlso : '&&';
TOrElse : '||';
TPlusPlus : '++';
TMinusMinus : '--';
TPlus : '+';
TMinus : '-';
TTilde : '~';
TExclamation : '!';
TSlash : '/';
TAsterisk : '*';
TPercent : '%';
TArrow : '=>';
TLessThanOrEqual : '<=';
TGreaterThanOrEqual : '>=';
TEqual : '==';
TNotEqual : '!=';
TAsteriskAssign : '*=';
TSlashAssign : '/=';
TPercentAssign : '%=';
TPlusAssign : '+=';
TMinusAssign : '-=';
TLeftShiftAssign : '<<=';
TRightShiftAssign : '>>=';
TAmpersandAssign : '&=';
THatAssign : '^=';
TBarAssign : '|=';

//IdentifierGeneral : IdentifierGeneralBegin IdentifierGeneralCharacter*;
$Token(kind=Identifier, defaultIdentifier=true)
IdentifierNormal : IdentifierBegin IdentifierCharacter*;
IdentifierVerbatim : '@' IdentifierBegin IdentifierCharacter*;
//IdentifierVerbatimStart : '@[' -> more, mode(VERBATIM_IDENTIFIER);
fragment IdentifierBegin : [a-zA-Z_];
fragment IdentifierCharacter : [a-zA-Z0-9_];
fragment IdentifierVerbatimCharacter : ~[\]] | IdentifierVerbatimEscape;
fragment IdentifierVerbatimEscape : '\\\\' | '\\]';
fragment IdentifierGeneralBegin : [a-zA-Z_];
fragment IdentifierGeneralCharacter : [a-zA-Z0-9_];

$Token(kind=Number)
LInteger : DecimalDigits | Hexadecimal;
$Token(kind=Number)
LDecimal : DecimalDigit+ '.' DecimalDigit+;
$Token(kind=Number)
LScientific : LDecimal [eE] Sign? DecimalDigit+;
fragment DecimalDigits : DecimalDigit+;
fragment DecimalDigit : [0-9];
fragment Sign : '+' | '-';
fragment Hexadecimal : ('0x'|'0X') HexDigit*;
fragment HexDigit : [0-9a-fA-F];

$Token(kind=Number)
LDateTimeOffset : LDate 'T' LTime TimeZone;
$Token(kind=Number)
LDateTime : LDate 'T' LTime;
$Token(kind=Number)
LDate : Sign? DateYear '-' DateMonth '-' DateDay;
$Token(kind=Number)
LTime : TimeHourMinute ':' TimeSecond;

fragment DateDay
    : '01' | '02' | '03' | '04' | '05' | '06' | '07' | '08' | '09' | '10'
    | '11' | '12' | '13' | '14' | '15'
    | '16' | '17' | '18' | '19' | '20' | '21' | '22' | '23' | '24' | '25'
    | '26' | '27' | '28' | '29' | '30'
    | '31';
fragment DateMonth 
    : '01' | '02' | '03' | '04' | '05' | '06' | '07' | '08' | '09' | '10' 
    | '11' | '12';
fragment DateYear : DecimalDigit DecimalDigit DecimalDigit DecimalDigit;
fragment TimeZone: Sign OffsetTimeHourMinute | 'Z';
fragment OffsetTimeHour
    : '00' | '01' | '02' | '03' | '04' | '05' | '06' | '07' | '08' | '09'
    | '10' | '11' | '12' | '13' | '14';
fragment OffsetTimeHourMinute: OffsetTimeHour ':' TimeMinute;
fragment TimeHour
    : '00' | '01' | '02' | '03' | '04' | '05' | '06' | '07' | '08' | '09'
    | '10' | '11'
    | '12' | '13' | '14' | '15' | '16' | '17' | '18' | '19' | '20' | '21'
    | '22' | '23';
fragment TimeHourMinute: TimeHour ':' TimeMinute;
fragment TimeMinute: [0-5] DecimalDigit;
fragment TimeSecond: [0-5] DecimalDigit TimeSecondDecimalPart?;
fragment TimeSecondDecimalPart: '.' DecimalDigits;

$Token(kind=String)
LRegularString
    : '"' DoubleQuoteTextCharacter* '"'
    | '\'' SingleQuoteTextCharacter* '\'';

DoubleQuoteVerbatimStringLiteralStart : '@"' -> more, mode(DOUBLEQUOTE_VERBATIM_STRING);
SingleQuoteVerbatimStringLiteralStart : '@\'' -> more, mode(SINGLEQUOTE_VERBATIM_STRING);
fragment SingleQuoteTextCharacter 
    : SingleQuoteTextSimple | CharacterEscapeSimple | CharacterEscapeUnicode;
fragment SingleQuoteTextSimple 
    : ~('\'' | '\\' | '\u000A' | '\u000D' | '\u0085' | '\u2028' | '\u2029');
fragment SingleQuoteTextVerbatimCharacter 
    : ~'\'' | SingleQuoteTextVerbatimCharacterEscape;
fragment SingleQuoteTextVerbatimCharacterEscape : '\'' '\'';
fragment SingleQuoteTextVerbatimCharacters : SingleQuoteTextVerbatimCharacter+;
fragment DoubleQuoteTextCharacter 
    : DoubleQuoteTextSimple | CharacterEscapeSimple | CharacterEscapeUnicode;
fragment DoubleQuoteTextSimple 
    : ~('"' | '\\' | '\u000A' | '\u000D' | '\u0085' | '\u2028' | '\u2029');
fragment DoubleQuoteTextVerbatimCharacter 
    : ~'"' | DoubleQuoteTextVerbatimCharacterEscape;
fragment DoubleQuoteTextVerbatimCharacterEscape : '"' '"';
fragment DoubleQuoteTextVerbatimCharacters : DoubleQuoteTextVerbatimCharacter+;
fragment CharacterEscapeSimple : '\\' CharacterEscapeSimpleCharacter;
fragment CharacterEscapeSimpleCharacter 
    : '\'' | '"' | '\\' | '0' | 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v'; 
fragment CharacterEscapeUnicode
    : '\\u' HexDigit HexDigit HexDigit HexDigit
    | '\\U' HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit;

$Token(kind=String)
LGuid : '#[' HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit
                   HexDigit '-' HexDigit HexDigit HexDigit HexDigit '-'
                   HexDigit HexDigit HexDigit HexDigit '-'
                   HexDigit HexDigit HexDigit HexDigit '-'
                   HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit
                   HexDigit HexDigit HexDigit HexDigit HexDigit ']';

// Whitespace and comments
$Token(kind=Whitespace)
LUtf8Bom : [\u00EF][\u00BB][\u00BF] -> channel(WHITESPACE);
$Token(kind=Whitespace, defaultWhitespace=true)
LWhiteSpace : [\u0020\u0009\u000B\u000C\u00A0\u001A]+ -> channel(WHITESPACE);
$Token(kind=Whitespace, endOfLine=true, defaultEndOfLine=true)
LCrLf : '\r'? '\n' -> channel(WHITESPACE);
$Token(kind=Whitespace, endOfLine=true)
LLineEnd : [\u0085\u2028\u2029] -> channel(WHITESPACE);

$Token(kind=GeneralComment)
LSingleLineComment : '//' ~[\r\n]* -> channel(COMMENT);
LCommentStart : '/*' -> more, mode(LMultilineComment);

$Token(kind=GeneralComment)
mode LMultilineComment;

LComment_CrLf : '\r'? '\n' -> more;
LComment_LineBreak : [\u0085\u2028\u2029] -> more;
LComment_Text : ~[\u002A\r\n\u0085\u2028\u2029]+ -> more;
$Token(kind=GeneralComment)
LComment : '*/' -> mode(DEFAULT_MODE), channel(COMMENT);
LComment_Star : '*' -> more;

$Token(kind=String)
mode DOUBLEQUOTE_VERBATIM_STRING;

DoubleQuoteVerbatimStringText : DoubleQuoteTextVerbatimCharacter -> more;
$Token(kind=String)
LDoubleQuoteVerbatimString : '"' -> mode(DEFAULT_MODE);

$Token(kind=String)
mode SINGLEQUOTE_VERBATIM_STRING;

SingleQuoteVerbatimStringText : SingleQuoteTextVerbatimCharacter -> more;
$Token(kind=String)
LSingleQuoteVerbatimString : '\'' -> mode(DEFAULT_MODE);
