lexer grammar CompilerLexer;

options
{
	generateCompiler=true;
}

channels
{
	COMMENT,
	WHITESPACE
}

// Keywords
$Token(first=KNamespace,last=KEof,kind=ReservedKeyword)
KNamespace : 'namespace';
KUsing : 'using';
KGrammar : 'grammar';
KOptions : 'options';
KFragment : 'fragment';
KHidden : 'hidden';
KDefines : 'defines';
KTrue : 'true';
KFalse : 'false';
KNull : 'null';
KEof : 'EOF';

// Tokens
TSemicolon : ';';
TArrow : '->';
TDot : '.';
TNonGreedyZeroOrOne : '??';
TNonGreedyZeroOrMore : '*?';
TNonGreedyOneOrMore : '+?';
TZeroOrOne : '?';
TZeroOrMore : '*';
TOneOrMore : '+';
TNegate : '~';
TAssign : '=';
TQuestionAssign : '?=';
TNegatedAssign : '!=';
TPlusAssign : '+=';
TBar : '|';
TAmpersand : '&';
TColon : ':';
$Token(defaultSeparator=true)
TComma : ',';
TOpenParen : '(';
TCloseParen : ')';
TOpenBracket : '[';
TCloseBracket : ']';
TOpenBrace : '{';
TCloseBrace : '}';
TLessThan : '<';
TGreaterThan : '>';

$Token(kind=Identifier, defaultIdentifier=true)
LexerIdentifier : '@'? LexerIdentifierBegin IdentifierCharacter*;
$Token(kind=Identifier)
ParserIdentifier : '@'? ParserIdentifierBegin IdentifierCharacter*;
fragment LexerIdentifierBegin : [A-Z];
fragment ParserIdentifierBegin : [a-z];
fragment IdentifierCharacter : [a-zA-Z0-9_];

$Token(kind=Number)
LInteger : DecimalDigits | Hexadecimal;
$Token(kind=Number)
LDecimal : DecimalDigit+ '.' DecimalDigit+;
$Token(kind=Number)
LScientific : LDecimal [eE] Sign? DecimalDigit+;
fragment DecimalDigits : DecimalDigit+;
fragment DecimalDigit : [0-9];
fragment Sign : '+' | '-';
fragment Hexadecimal : ('0x'|'0X') HexDigit*;
fragment HexDigit : [0-9a-fA-F];

$Token(kind=String)
LString
    : DoubleQuoteString
    | SingleQuoteString
    | DoubleQuoteVerbatimString
    | SingleQuoteVerbatimString;

fragment DoubleQuoteString : '"' DoubleQuoteTextCharacter* '"';
fragment SingleQuoteString : '\'' SingleQuoteTextCharacter* '\'';
fragment DoubleQuoteVerbatimString : '@"' DoubleQuoteTextVerbatimCharacter* '"';
fragment SingleQuoteVerbatimString : '@\'' SingleQuoteTextVerbatimCharacter* '\'';
fragment SingleQuoteTextCharacter 
    : SingleQuoteTextSimple | CharacterEscapeSimple | CharacterEscapeUnicode;
fragment SingleQuoteTextSimple 
    : ~('\'' | '\\' | '\u000A' | '\u000D' | '\u0085' | '\u2028' | '\u2029');
fragment SingleQuoteTextVerbatimCharacter 
    : ~'\'' | SingleQuoteTextVerbatimCharacterEscape;
fragment SingleQuoteTextVerbatimCharacterEscape : '\'' '\'';
fragment SingleQuoteTextVerbatimCharacters : SingleQuoteTextVerbatimCharacter+;
fragment DoubleQuoteTextCharacter 
    : DoubleQuoteTextSimple | CharacterEscapeSimple | CharacterEscapeUnicode;
fragment DoubleQuoteTextSimple 
    : ~('"' | '\\' | '\u000A' | '\u000D' | '\u0085' | '\u2028' | '\u2029');
fragment DoubleQuoteTextVerbatimCharacter 
    : ~'"' | DoubleQuoteTextVerbatimCharacterEscape;
fragment DoubleQuoteTextVerbatimCharacterEscape : '"' '"';
fragment DoubleQuoteTextVerbatimCharacters : DoubleQuoteTextVerbatimCharacter+;
fragment CharacterEscapeSimple : '\\' CharacterEscapeSimpleCharacter;
fragment CharacterEscapeSimpleCharacter 
    : '\'' | '"' | '\\' | '0' | 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v'; 
fragment CharacterEscapeUnicode
    : '\\u' HexDigit HexDigit HexDigit HexDigit
    | '\\U' HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit;

// Whitespace and comments
$Token(kind=Whitespace)
LUtf8Bom : [\u00EF][\u00BB][\u00BF] -> channel(WHITESPACE);
$Token(kind=Whitespace, defaultWhitespace=true)
LWhiteSpace : [\u0020\u0009\u000B\u000C\u00A0\u001A]+ -> channel(WHITESPACE);
$Token(kind=Whitespace, endOfLine=true, defaultEndOfLine=true)
LCrLf : '\r'? '\n' -> channel(WHITESPACE);
$Token(kind=Whitespace, endOfLine=true)
LLineEnd : [\u0085\u2028\u2029] -> channel(WHITESPACE);

$Token(kind=GeneralComment)
LSingleLineComment : '//' ~[\r\n]* -> channel(COMMENT);
$Token(kind=GeneralComment)
LMultilineComment : '/*' .*? '*/' -> channel(COMMENT);
