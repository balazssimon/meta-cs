parser grammar MetaParser;

@header 
{
using MetaDslx.CodeAnalysis.Symbols;
using MetaDslx.Languages.Meta.Model;
}

options
{
    tokenVocab = MetaLexer; 
	generateCompiler=true;
}

main: usingNamespace* namespaceDeclaration EOF;

$Name
name : identifier;

$Name
qualifiedName : qualifier;

$Qualifier
qualifier : identifier (TDot identifier)*;

$Property(Attributes)
$Use(MetaAttribute)
attribute : TOpenBracket qualifier TCloseBracket;

$Import
usingNamespace: KUsing qualifier TSemicolon;

$Define(type=MetaNamespace,nestingProperty=Declarations,merge=true)
namespaceDeclaration: attribute* KNamespace qualifiedName namespaceBody;

$Scope
namespaceBody : TOpenBrace usingNamespace* metamodelDeclaration declaration* TCloseBrace;

$Property(DefinedMetaModel)
$Define(MetaModel)
$Property(Documentation)
$Documentation
metamodelDeclaration: attribute* KMetamodel name (TOpenParen metamodelPropertyList? TCloseParen)? TSemicolon;

metamodelPropertyList : metamodelProperty (TComma metamodelProperty)*;

metamodelProperty : metamodelUriProperty | metamodelPrefixProperty;

$Property(Uri)
metamodelUriProperty : IUri TAssign $Value stringLiteral;
$Property(Prefix)
metamodelPrefixProperty : IPrefix TAssign $Value stringLiteral;

$Property(Declarations) 
declaration : enumDeclaration | classDeclaration | associationDeclaration | constDeclaration;

$Define(MetaEnum)
$Property(Documentation)
$Documentation
enumDeclaration : attribute* KEnum name enumBody;
$Scope
enumBody : TOpenBrace $Property(EnumLiterals) enumValues (TSemicolon enumMemberDeclaration*)? TCloseBrace;
enumValues : enumValue (TComma enumValue)*;
$Define(MetaEnumLiteral)
$Property(Documentation)
$Documentation
enumValue : attribute* name;
enumMemberDeclaration : $Property(Operations) operationDeclaration;

$Define(MetaClass)
$Property(Documentation)
$Documentation
classDeclaration : attribute* symbolTypeAttribute? $Property(name=IsAbstract,value=true) KAbstract? KClass name (TColon $Property(SuperClasses) classAncestors)? classBody;
$Property(SymbolType)
$SymbolType
symbolTypeAttribute : TOpenBracket KSymbol TColon qualifier TCloseBracket;
$Scope
classBody : TOpenBrace classMemberDeclaration* TCloseBrace;
classAncestors : classAncestor (TComma classAncestor)*;
classAncestor : $Use(type=MetaClass) qualifier;
classMemberDeclaration 
	: $Property(Properties) fieldDeclaration 
	| $Property(Operations) operationDeclaration
	;

$Define(MetaProperty)
$Property(Documentation)
$Documentation
fieldDeclaration : attribute* fieldSymbolPropertyAttribute? fieldContainment? fieldModifier? $Property(Type) typeReference name defaultValue? redefinitionsOrSubsettings* TSemicolon;
$Property(SymbolProperty)
fieldSymbolPropertyAttribute : TOpenBracket KSymbol TColon $SymbolProperty identifier TCloseBracket;
$Property(name=IsContainment,value=true)
fieldContainment : KContainment;
$Property(Kind)
fieldModifier 
	: $Value(MetaPropertyKind.Readonly) KReadonly 
	| $Value(MetaPropertyKind.Lazy) KLazy 
	| $Value(MetaPropertyKind.Derived) KDerived
	| $Value(MetaPropertyKind.DerivedUnion) KUnion
	;
$Property(DefaultValue)
defaultValue : TAssign $Value stringLiteral;

redefinitionsOrSubsettings : redefinitions | subsettings;
redefinitions : KRedefines $Property(RedefinedProperties) nameUseList?;
subsettings : KSubsets $Property(SubsettedProperties) nameUseList?;

$Use(MetaProperty)
nameUseList : qualifier (TComma qualifier)*;

$Define(MetaConstant)
constDeclaration : KConst $Property(Type) typeReference name constValue? TSemicolon;
$Property(DotNetName)
constValue : TAssign $Value stringLiteral;

$Use(MetaType)
returnType : typeReference | voidType;
$Use(MetaType)
typeOfReference : typeReference;
$Use(MetaType)
typeReference : collectionType | simpleType;
$Use(MetaType)
simpleType : primitiveType | objectType | nullableType | classType;

$Use(types=(MetaClass,MetaEnum,MetaConstant))
classType : qualifier;

$Identifier
objectType 
	: KObject 
	| KSymbol
	| KString
	;

$Identifier
primitiveType 
	: KInt 
	| KLong 
	| KFloat 
	| KDouble 
	| KByte 
	| KBool
	;

$Identifier
voidType 
	: KVoid
	;

$Define(MetaNullableType)
nullableType : $Property(InnerType) primitiveType TQuestion;

$Define(MetaCollectionType)
collectionType : $Property(Kind) collectionKind TLessThan $Property(InnerType) simpleType TGreaterThan;
collectionKind 
	: $Value(MetaCollectionKind.Set) KSet 
	| $Value(MetaCollectionKind.List) KList
	| $Value(MetaCollectionKind.MultiSet) KMultiSet 
	| $Value(MetaCollectionKind.MultiList) KMultiList
	;
	
$Define(MetaOperation)
$Property(Documentation)
$Documentation
operationDeclaration : attribute* operationModifier* $Property(ReturnType) returnType name TOpenParen $Property(Parameters) parameterList? TCloseParen TSemicolon;

operationModifier : operationModifierBuilder | operationModifierReadonly;
$Property(name=IsBuilder,value=true) 
operationModifierBuilder : KBuilder;
$Property(name=IsReadonly,value=true)
operationModifierReadonly : KReadonly;

parameterList : parameter (TComma parameter)*;

$Define(MetaParameter)
parameter : attribute* $Property(Type) typeReference name;

$Opposite
associationDeclaration : attribute* KAssociation $Use(MetaProperty) source=qualifier KWith $Use(MetaProperty) target=qualifier TSemicolon;


// Additional rules for lexer:

// Identifiers
$Identifier
identifier 
	: IdentifierNormal 
	| IdentifierVerbatim
	| IUri
	| IPrefix
	;

// Literals
literal 
    : nullLiteral
	| booleanLiteral
	| integerLiteral
	| decimalLiteral
	| scientificLiteral
    | stringLiteral
	;

// Null literal
$Value
nullLiteral : KNull;

// Boolean literals
$Value
booleanLiteral : KTrue | KFalse;

// Number literals
$Value
integerLiteral : LInteger;
$Value
decimalLiteral : LDecimal;
$Value
scientificLiteral : LScientific;

// String literals
$Value
stringLiteral : LRegularString;
